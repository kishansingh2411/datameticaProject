--######################################################################################################################
--#   This source code is the property of:
--#   Cablevision Systems Corporation, Inc. (c) 2015
--#   1111 Stewart Avenue, Bethpage, NY 11714
--#   www.cablevision.com
--#   Department: AM
--#
--#   Program name: module-transform.pig
--#   Program type: Pig script
--#   Purpose:    : Loading data from incoming_stg_vma_nsn_call_usage_tbl table to work_a_vma_nsn_call_usage_tbl table
--#   Author(s)   : DataMetica Team
--#   Usage       : pig -p var1=var1 -useHCatalog module-transform.pig
--#   Date        : 12/19/2016
--#   Log File    : .../log/ovcdr/${job_name}.log
--#   SQL File    : 
--#   Error File  : .../log/ovcdr/${job_name}.log
--#   Dependency  : 
--#   Disclaimer  : 
--#
--#   Modification History :
--#   =======================
--#
--#    ver     who                      date             comments
--#   ------   --------------------     ----------       -------------------------------------
--#    1.0     DataMetica Team          12/19/2016       Initial version
--#
--#
--#####################################################################################################################
--#                                   Load                                     #
--##############################################################################

incoming_stg_data = 
	LOAD '${hive_database_name_incoming}.${incoming_stg_tbl}' 
	USING org.apache.hive.hcatalog.pig.HCatLoader()
;

work_chc_data = 
	LOAD '${hive_database_name_work}.${work_chc_tbl}' 
	USING org.apache.hive.hcatalog.pig.HCatLoader()
;
	   
--##############################################################################
--#                                 Filter                                     #
--##############################################################################

incoming_stg_data_filter = 
	FILTER incoming_stg_data 
    BY (source_date == '$source_date' )
;

--##############################################################################
--#         Generate Corp, Dwelling_nrb, Cust                                  #
--##############################################################################

incoming_stg_data_filter_join =
   JOIN incoming_stg_data_filter BY (usage_date,telephone_number) LEFT,
   work_chc_data BY (usage_date,telephone_number)
;
   
--##############################################################################
--#                                 Transform                                  #
--##############################################################################
	
generated_records = 
	FOREACH incoming_stg_data_filter_join 
	GENERATE
        incoming_stg_data_filter::usage_date                                                              AS usage_date,
		incoming_stg_data_filter::telephone_number                                                        AS telephone_number,
        ((incoming_stg_data_filter::on_net_ind is null)?'-':UPPER(incoming_stg_data_filter::on_net_ind))  AS on_net_ind,
        ((incoming_stg_data_filter::number_of_calls is null)?0:incoming_stg_data_filter::number_of_calls) AS number_of_calls,
		incoming_stg_data_filter::batch_id                									              AS batch_nmbr,	
		((incoming_stg_data_filter::attempt_cnt is null)?0:incoming_stg_data_filter::attempt_cnt)         AS attempt_count,
		((work_chc_data::corp is null)?'-1' :work_chc_data::corp)           		                      AS corp,
		((work_chc_data::house is null)? '-1':work_chc_data::house)                                       AS house,
		((work_chc_data::cust is null)? '-1' :work_chc_data::cust)                                        AS cust,
		CurrentTime()                                                                                     AS dtm_created,
		CurrentTime()                                                                                     AS dtm_last_modified,
		'${file_name}'                                                                                    AS file_name
; 

--##############################################################################
--#                                 Store                                      #
--##############################################################################

  STORE generated_records 
   INTO '${hive_database_name_work}.${work_target_tbl}' 
   USING org.apache.hive.hcatalog.pig.HCatStorer('usage_date=$usage_date)
; 
				
--##############################################################################
--#                              End                                           #
--##############################################################################