--######################################################################################################################
--#   This source code is the property of:
--#   Cablevision Systems Corporation, Inc. (c) 2015
--#   1111 Stewart Avenue, Bethpage, NY 11714
--#   www.cablevision.com
--#   Department: AM
--#
--#   Program name: module-transform.pig
--#   Program type: Pig script
--#   Purpose:    : Loading data from incoming_hit_data table to gold_opt_hit_data table
--#   Author(s)   : DataMetica Team
--#   Usage       : pig -p var1=var1 -useHCatalog module-transform.pig
--#   Date        : 12/28/2015
--#   Log File    : .../log/ods/ODS_GOLD_CUSTOMER_DATA_JOB.log
--#   SQL File    : 
--#   Error File  : .../log/ods/ODS_GOLD_CUSTOMER_DATA_JOB.log
--#   Dependency  : 
--#   Disclaimer  : 
--#
--#   Modification History :
--#   =======================
--#
--#    ver     who                      date             comments
--#   ------   --------------------     ----------       -------------------------------------
--#    1.0     DataMetica Team          12/28/2015       Initial version
--#
--#
--#####################################################################################################################

register ${udf_jar_path}/pig-udf-bank-${version}.jar;
define Stitch org.apache.pig.piggybank.evaluation.Stitch;
define Over org.apache.pig.piggybank.evaluation.Over('int');

define dateDifferCount com.cablevision.edh.udf.DateDifferCount();

incoming_h_optimum_user = 
	LOAD '${source_database}.${source_table}' 
    USING org.apache.hive.hcatalog.pig.HCatLoader()
;

SPLIT incoming_h_optimum_user
 INTO incoming_h_optimum_user_active
IF(
       DaysBetween(ToDate('$source_date','yyyy-MM-dd'),dtm_efftv) >= 0
       AND
       DaysBetween(dtm_expired ,ToDate('$source_date','yyyy-MM-dd')) >= 0
   ),
   incoming_h_optimum_user_inactive OTHERWISE
;


incoming_h_optimum_user_active_group = 
   GROUP incoming_h_optimum_user_active BY (optimum_id)
;

incoming_h_optimum_user_active_derive_row_number = 
   FOREACH incoming_h_optimum_user_active_group
   {
      incoming_h_optimum_user_active_order = 
         ORDER incoming_h_optimum_user_active 
         BY dtm_efftv DESC,
         h_optimum_user_id DESC
      ;
      GENERATE 
      FLATTEN(Stitch(incoming_h_optimum_user_active_order,Over(incoming_h_optimum_user_active_order,'row_number')));
   }
;

incoming_h_optimum_user_active_derive_row_number_generated = 
   FOREACH incoming_h_optimum_user_active_derive_row_number 
   GENERATE
   stitched::h_optimum_user_id         AS h_optimum_user_id,
   stitched::optimum_user_id           AS optimum_user_id,
   stitched::customer_account_id       AS customer_account_id,
   stitched::dtm_efftv                 AS dtm_efftv,
   stitched::dtm_expired               AS dtm_expired,
   stitched::dtm_created               AS dtm_created,
   stitched::created_by_process        AS created_by_process,
   stitched::sourced_from_system       AS sourced_from_system,
   stitched::last_updated_by_process   AS last_updated_by_process,
   stitched::last_updated_from_system  AS last_updated_from_system,
   stitched::dtm_last_updated          AS dtm_last_updated,		
   stitched::dtm_last_modified         AS dtm_last_modified,		
   stitched::id_cust                   AS id_cust,					
   stitched::id_service_rec            AS id_service_rec,			
   stitched::id_user                   AS id_user,
   stitched::corp                      AS corp,
   stitched::dwelling_nbr              AS dwelling_nbr,
   stitched::cust                      AS cust,
   stitched::dtm_start_date            AS dtm_start_date,
   stitched::dtm_end_date              AS dtm_end_date,
   stitched::email_ind                 AS email_ind,
   stitched::id_domain                 AS id_domain,
   stitched::optimum_id                AS optimum_id,
   stitched::mixed_case_optimum_id     AS mixed_case_optimum_id,
   stitched::id_user_type              AS id_user_type,
   stitched::user_type_descr           AS user_type_descr,
   stitched::source_appl               AS source_appl,
   stitched::result                    AS row_number
;

incoming_h_optimum_user_active_unique = 
   FILTER incoming_h_optimum_user_active_derive_row_number_generated BY (row_number == 1)
;

incoming_h_optimum_user_inactive_group = 
   GROUP incoming_h_optimum_user_inactive BY (optimum_id)
;

incoming_h_optimum_user_inactive_derive_row_number = 
   FOREACH incoming_h_optimum_user_inactive_group
   {
      incoming_h_optimum_user_inactive_order = 
         ORDER incoming_h_optimum_user_inactive 
         BY dtm_efftv DESC,
         h_optimum_user_id DESC
      ;
      GENERATE 
      FLATTEN(Stitch(incoming_h_optimum_user_inactive_order,Over(incoming_h_optimum_user_inactive_order,'row_number')));
   }
;

incoming_h_optimum_user_inactive_derive_row_number_generated = 
   FOREACH incoming_h_optimum_user_inactive_derive_row_number 
   GENERATE
   stitched::h_optimum_user_id         AS h_optimum_user_id,
   stitched::optimum_user_id           AS optimum_user_id,
   stitched::customer_account_id       AS customer_account_id,
   stitched::dtm_efftv                 AS dtm_efftv,
   stitched::dtm_expired               AS dtm_expired,
   stitched::dtm_created               AS dtm_created,
   stitched::created_by_process        AS created_by_process,
   stitched::sourced_from_system       AS sourced_from_system,
   stitched::last_updated_by_process   AS last_updated_by_process,
   stitched::last_updated_from_system  AS last_updated_from_system,
   stitched::dtm_last_updated          AS dtm_last_updated,		
   stitched::dtm_last_modified         AS dtm_last_modified,		
   stitched::id_cust                   AS id_cust,					
   stitched::id_service_rec            AS id_service_rec,			
   stitched::id_user                   AS id_user,
   stitched::corp                      AS corp,
   stitched::dwelling_nbr              AS dwelling_nbr,
   stitched::cust                      AS cust,
   stitched::dtm_start_date            AS dtm_start_date,
   stitched::dtm_end_date              AS dtm_end_date,
   stitched::email_ind                 AS email_ind,
   stitched::id_domain                 AS id_domain,
   stitched::optimum_id                AS optimum_id,
   stitched::mixed_case_optimum_id     AS mixed_case_optimum_id,
   stitched::id_user_type              AS id_user_type,
   stitched::user_type_descr           AS user_type_descr,
   stitched::source_appl               AS source_appl,
   stitched::result                    AS row_number
;

incoming_h_optimum_user_inactive_unique = 
   FILTER incoming_h_optimum_user_inactive_derive_row_number_generated BY (row_number == 1)
;

incoming_h_optimum_user_union =
   UNION ONSCHEMA incoming_h_optimum_user_active_unique,
   incoming_h_optimum_user_inactive_unique
;

gold_h_optimum_user = 
   FOREACH incoming_h_optimum_user_union
   GENERATE h_optimum_user_id
   ..
   source_appl
;

STORE gold_h_optimum_user
   INTO '${target_database}.${target_table}'
   USING org.apache.hive.hcatalog.pig.HCatStorer()
;

--##############################################################################
--#                              End                                           #
--##############################################################################